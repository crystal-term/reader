require "term-cursor"
require "term-screen"

require "./reader/history"
require "./reader/line"
require "./reader/key_event"
require "./reader/console"
require "./reader/version"

module Term
  class Reader
    # Raised when a user hits ctrl-c
    class InputInterrupt < Exception; end

    alias HandlerFunc = String, KeyEvent -> Nil

    # Key codes
    CARRIAGE_RETURN = 13
    NEWLINE         = 10
    BACKSPACE       =  8
    DELETE          = 27

    getter input : IO
    getter output : IO
    getter env : Hash(String, String)

    # Do we want to keep a log of things as they happen
    getter? track_history : Bool
    getter console : Console
    getter cursor : Term::Cursor.class
    getter history : History
    getter interrupt : Symbol

    @event_handlers : Hash(String, Array(HandlerFunc))

    # :nodoc:
    class_property global_handlers : Hash(String, Array(HandlerFunc)) = Hash(String, Array(HandlerFunc)).new { |h, k|
      h[k] = [] of HandlerFunc
    }

    def initialize(@input : IO = STDIN,
                   @output : IO = STDOUT,
                   @env : Hash(String, String) = ENV.to_h,
                   @interrupt : Symbol = :error,
                   @track_history : Bool = true,
                   @history_cycle : Bool = false,
                   @history_exclude : String -> Bool = ->(s : String) { s.strip.empty? },
                   @history_duplicates : Bool = false)
      @console = if @input.is_a?(IO::FileDescriptor)
                   Console.new(@input.as(IO::FileDescriptor))
                 else
                   # For testing with non-FileDescriptor IO (like IO::Memory)
                   Console.new(STDIN)
                 end
      @event_handlers = Hash(String, Array(HandlerFunc)).new do |h, k|
        h[k] = [] of HandlerFunc
      end

      @history = History.new do |h|
        h.cycle = @history_cycle
        h.duplicates = @history_duplicates
        h.exclude = @history_exclude
      end

      @stop = false
      @cursor = Term::Cursor

      Term::Reader.subscribe(:ctrl_d, :ctrl_z)
    end

    # Listen for specific keys (or all keys if `keys` is empty)
    def on_key(*keys : String | Symbol, &block : HandlerFunc) : Nil
      on_key keys, &block
    end

    # :ditto:
    def on_key(keys : Enumerable(String | Symbol) = [] of Symbol, &block : HandlerFunc) : Nil
      if keys.empty?
        @event_handlers[""] << block
      else
        keys.each do |key|
          @event_handlers[key.to_s] << block
        end
      end
    end

    # Get input in unbuffered mode.
    def unbuffered(& : ->)
      buffering = @output.sync?
      # Immidiately flush output
      @output.sync = true
      yield
    ensure
      @output.sync = buffering
    end

    # Reads a keypress, including ivisible multibyte codes
    # and return a character as a String.
    #
    # Nothing is echoed to the console. This call will block for
    # a single keypress, but will not wait for Enter to be pressed.
    def read_keypress(echo : Bool = false, raw : Bool = true, nonblock : Bool = false, interrupt : Symbol | Proc? = nil) : String?
      codes = unbuffered { get_codes(echo, raw, nonblock, interrupt || @interrupt) }
      char = codes.try &.map(&.chr).join

      trigger_key_event(char) if char
      char
    end

    # Get input code points
    # FIXME: Fails to handle escape '\e' all by itself
    def get_codes(echo : Bool, raw : Bool, nonblock : Bool, interrupt : Symbol | Proc = @interrupt) : Array(Int32)?
      # For non-FileDescriptor input (like IO::Memory for testing), read directly
      if !@input.is_a?(IO::FileDescriptor)
        char = @input.read_char
        return nil if char.nil?
        handle_interrupt(interrupt) if console.keys[char.to_s]? == "ctrl_c"
        return [char.ord] of Int32
      end

      char = console.get_char(echo, raw, nonblock)
      handle_interrupt(interrupt) if console.keys[char.to_s]? == "ctrl_c"
      return nil if char.nil?

      codes = [char.ord] of Int32
      condition = ->(escape : Array(UInt8)) do
        (codes - escape).empty? ||
        (escape - codes).empty? &&
        !(64..126).covers?(codes.last)
      end

      while console.escape_codes.any?(condition)
        char_codes = get_codes(echo, raw, true)
        break if char_codes.nil?
        codes.concat char_codes
      end

      codes
    end

    # Get a signal line from STDIN. Each key pressed is echoed
    # back to the shell. The input terminates when enter or
    # return key is pressed.
    def read_line(*, prompt : String = "", value : String = "", echo : Bool = true,
                  raw : Bool = true, nonblock : Bool = false) : String
      line = Line.new(value, prompt: prompt)
      screen_width = Term::Screen.width

      # Print the initial prompt/line
      @output.print(line.to_s)

      loop do
        codes = get_codes(echo, raw, nonblock)
        break unless codes && !codes.empty?

        code = codes[0]
        char = codes.map(&.chr).join

        if {"ctrl_z", "ctrl_d"}.includes?(console.keys[char]?)
          trigger_key_event(char, line: line.to_s)
          break
        end

        if raw && echo
          clear_display(line, screen_width)
        end

        case console.keys[char]?.to_s
        when "backspace", BACKSPACE == code
          if !line.start?
            line.left
            line.delete
          end
        when "delete", DELETE == code
          line.delete
        when /ctrl_/
          # skip
        when "up"
          line.replace(history_previous.to_s) if history_previous?
        when "down"
          line.replace(history_next? ? history_next.to_s : "")
        when "left"
          line.left
        when "right"
          line.right
        when "home"
          line.move_to_start
        when "end"
          line.move_to_end
        else
          if raw && code == CARRIAGE_RETURN
            char = "\n"
            line.move_to_end
          end
          line.insert(char)
        end

        if console.keys[char]? == "backspace" || BACKSPACE == code && echo
          if raw
            output.print("\e[1X") unless line.start?
          else
            output.print(" " + (line.start? ? "" : "\b"))
          end
        end

        # Trigger before line is printed to allow for changes
        trigger_key_event(char, line: line.to_s)

        if raw && echo
          # Don't redraw the line when Enter is pressed to avoid duplication
          unless char == "\n"
            output.print(line.to_s)
            unless line.end? # readjust the cursor position
              output.print(cursor.backward(line.text_size - line.cursor))
            end
          else
            line.move_to_start
          end
        end

        if {CARRIAGE_RETURN, NEWLINE}.includes?(code)
          # For multiline with echo, cursor needs to move to next line
          # Only add newline if line doesn't already end with one
          if echo && raw
            unless line.text.ends_with?("\n")
              output.print("\n")
            end
          elsif !echo
            output.puts
          end
          break
        end

        if track_history? && echo
          add_to_history(line.text.strip)
        end
      end

      line.text
    end

    # Clear display for the current line input
    #
    # Handles clearing input that is longer than the current
    # terminal width, which allows copy + pasting long strings.
    def clear_display(line : Line, screen_width : Int32) : Nil
      total_lines = count_screen_lines(line.to_s, screen_width)
      current_line = count_screen_lines(line.prompt_size + line.cursor, screen_width)
      lines_down = total_lines - current_line

      output.print(cursor.down(lines_down)) unless lines_down.zero?
      output.print(cursor.clear_lines(total_lines))
    end

    # Count the number of screen lines the given line
    # takes up in the terminal.
    def count_screen_lines(line : String, screen_width : Int32) : Int32
      line_size = Line.sanitize(line).size
      count_screen_lines(line_size, screen_width)
    end

    # ditto
    def count_screen_lines(size : Int, screen_width : Int32) : Int32
      1 + [0, (size - 1) // screen_width].max
    end

    # Read multiple lines and return them as an array.
    # Skip empty lines in the returned lines array.
    # The input gathering is terminated by Ctrl+d or
    # Ctrl+z.
    def read_multiline(prompt : String = "") : Array(String)
      read_multiline(prompt) { }
    end

    # ditto
    def read_multiline(prompt : String = "", & : String ->) : Array(String)
      @stop = false
      lines = [] of String

      until @stop
        line = read_line(prompt: prompt)
        break if !line || line.strip.empty?
        next if line !~ /\S/ && !@stop
        lines << line
        yield line
      end

      lines
    end

    def keyctrl_d : Nil
      @stop = true
    end

    def keyctrl_z : Nil
      keyctrl_d
    end

    def add_to_history(line : String)
      @history << line
    end

    def history_next? : Bool
      @history.next?
    end

    def history_next : String?
      @history.next
      @history.get
    end

    def history_previous? : Bool
      @history.previous?
    end

    def history_previous : String?
      line = @history.get
      @history.previous
      line
    end

    # Inspect class name and public attributes
    def inspect(io : IO) : Nil
      io << "#<" << self.class
      io << ": @input=" << input
      io << ", @output=" << output
      io << '>'
    end

    private def unpack_array(arr : Array(Int32)) : String
      io = IO::Memory.new
      arr.each do |i|
        io.write_bytes(i)
      end
      io.rewind
      io.gets_to_end
    end

    # Publish event
    private def trigger_key_event(char : String, line : String = "") : Nil
      event = KeyEvent.from(console.keys, char, line)
      key = event.key.name

      (@event_handlers[key] +
        @event_handlers[""] +
        self.class.global_handlers[key] +
        self.class.global_handlers[""]).each do |proc|
        proc.call(event.key.name, event)
      end
    end

    # Handle input interrupt based on provided value
    private def handle_interrupt(interrupt : Symbol | Proc = @interrupt) : Nil
      case interrupt
      when :signal
        Process.signal(:int, Process.pid)
      when :exit
        exit(130)
      when Proc
        interrupt.as(Proc).call
      when :noop
        return
      else
        # Ctrl-C
        raise InputInterrupt.new("Ctrl-c was pressed")
      end
    end

    macro subscribe(*keys)
      {% valid_keys = (Term::Reader::CONTROL_KEYS.values + Term::Reader::KEYS.values).uniq %}
      {% for key in keys %}
        {% if key.id.symbolize == :keypress %}
          %kp = Term::Reader::HandlerFunc.new { |k, e| self.keypress(k, e); nil }
          Term::Reader.global_handlers[""] << %kp
        {% elsif valid_keys.includes?(key.id.stringify) %}
          %kp{key.id} = Term::Reader::HandlerFunc.new { |k, e| self.key{{ key.id }}; nil }
          Term::Reader.global_handlers[{{ key.id.stringify }}] << %kp{key.id}
        {% end %}
      {% end %}
    end
  end
end
